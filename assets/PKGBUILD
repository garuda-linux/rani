# Maintainer: dr460nf1r3 <root at dr460nf1r3 dot org>

pkgname=garuda-rani-git
pkgver=2.5.1.r30.g8c66368
pkgrel=1
_electronversion=36
_branch=refactor/electron-rewrite
_pkgname=garuda-rani
pkgdesc="Garuda's Reliable Assistant for Native Installations"
arch=('any')
url="https://gitlab.com/garuda-linux/applications/rani"
license=('GPL3')
depends=('bash' 'curl' "electron${_electronversion}" 'pacman-contrib' 'garuda-libs' 'garuda-update')
makedepends=('git' 'base-devel' 'gendesk')
optdepends=('paru: show pending AUR updates'
  'meld: compare pacdiff files via a GUI on GTK systems'
  'kompare: compare pacdiff files via a GUI on Qt systems'
  'pace: manage Pacman repositories via a GUI'
  'reflector-simple: update Arch mirrorlists interactively'
  'btrfs-assistant: easily manage Btrfs snapshots and further settings')
provides=('garuda-rani')
conflicts=('garuda-rani')
options=('!strip' '!emptydirs' '!lto' '!debug')
source=("git+$url.git#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd rani || exit
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd rani || exit

  # Verbose by default as preview version
  sed -i 's/logLevel = LogLevel.INFO/logLevel = LogLevel.VERBOSE/g' "$srcdir/rani/packages/renderer/src/app/logging/logging.ts"

  sed -i -e "
        s/@electronversion@/${_electronversion}/g
        s/@appname@/${_pkgname}/g
        s/@runname@/app.asar/g
        s/@cfgdirname@/${_pkgname}/g
        s/@options@/env ELECTRON_OZONE_PLATFORM_HINT=auto/g
    " "${srcdir}/rani/assets/garuda-rani.sh"

  export ELECTRON_SKIP_BINARY_DOWNLOAD=1
  export SYSTEM_ELECTRON_VERSION="$(electron${_electronversion} -v | sed 's/v//g')"
  export ELECTRON_OVERRIDE_DIST_PATH="/usr/lib/electron${_electronversion}"

  HOME="${srcdir}/.electron-gyp"
  mkdir -p "${srcdir}/.electron-gyp"
  sed -i "s/\"electron\": \"[^\"]*\"/\"electron\": \"${SYSTEM_ELECTRON_VERSION}\"/g" package.json
  NODE_ENV=development corepack pnpm -r install
}

build() {
  cd rani || exit
  local electronDist="/usr/lib/electron${_electronversion}"

  NODE_ENV=production corepack pnpm run build
  NODE_ENV=production corepack pnpm exec electron-builder --linux --dir \
    --config "${srcdir}/rani/electron-builder.mjs" \
    -c.electronDist="${electronDist}" \
    -c.electronVersion="${SYSTEM_ELECTRON_VERSION}"
}

package() {
  install -Dm755 "${srcdir}/rani/assets/garuda-rani.sh" "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm644 "${srcdir}/rani/dist/linux-unpacked/resources/app.asar" -t "${pkgdir}/usr/lib/${_pkgname}"
  cp -Pr --no-preserve=ownership "${srcdir}/rani/dist/linux-unpacked/resources/app.asar.unpacked" "${pkgdir}/usr/lib/${_pkgname}"
  install -Dm644 "${srcdir}/rani/assets/${_pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm644 "${srcdir}/rani/LICENSE" -t "${pkgdir}/usr/share/licenses/${_pkgname}"

    # Clean srcdir references from native modules
    if [ -d "${pkgdir}/usr/lib/${_pkgname}" ]; then
      # Strip debug symbols and clean paths from .node files
      find "${pkgdir}/usr/lib/${_pkgname}" -name "*.node" -exec strip {} + 2>/dev/null || true

      # Remove any files containing srcdir references
      grep -r "${srcdir}" "${pkgdir}/usr/lib/${_pkgname}" 2>/dev/null | cut -d: -f1 | sort -u | while read file; do
        echo "Cleaning srcdir reference from: $file"
        sed -i "s|${srcdir}|/usr/lib/${_pkgname}|g" "$file" 2>/dev/null || rm -f "$file"
      done
    fi
}
